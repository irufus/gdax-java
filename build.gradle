plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

allprojects {
    apply plugin: 'java'

    group = 'com.coinbase.exchange'
    description = 'Client for the Coinbase Pro API'
    version = '0.11.0'

    repositories {
        mavenCentral()
    }

    test {
        exclude '**/*IntegrationTest*'
    }

    /**
     * User can run these as optional against the sandbox api exchange
     **/
    task integrationTest(type: Test) {
        include '**/*IntegrationTest*'
    }
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.4.3')
    }
}

dependencies {
    implementation project(":api")
    implementation project(":model")
    implementation project(":security")
    implementation project(":websocketfeed")
}
